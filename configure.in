dnl -*- m4 -*-

#  $Archive:: /Ti/AMMPI/Makefile                                         $
#     $Date: 2004/02/04 07:26:47 $
# $Revision: 1.108 $
# Description: GASNet configure script
# Copyright 2002, Dan Bonachea <bonachea@cs.berkeley.edu>
# Based in part on the Titanium project configure script
# Terms of use are as specified in license.txt
AC_REVISION($Revision: 1.108 $)

# the "new" way
# AC_INIT(GASNet, 0.2)
# AC_CONFIG_SRCDIR(gasnet.h)
# AM_INIT_AUTOMAKE([no-define])

# the "old" way
AC_INIT(gasnet.h)
GASNET_FIX_SHELL
AC_CONFIG_AUX_DIR(config-aux)
 
AC_CANONICAL_SYSTEM
AC_VALIDATE_CACHED_SYSTEM_TUPLE
AM_INIT_AUTOMAKE(GASNet, 1.3, no-define)

AC_PREFIX_DEFAULT(/usr/local)
AM_CONFIG_HEADER(gasnet_config.h)

# Save useful directory names
TOP_SRCDIR=`cd ${srcdir} && pwd`
TOP_BUILDDIR=`pwd`
AC_SUBST(TOP_SRCDIR)
AC_SUBST(TOP_BUILDDIR)

GASNET_RESTORE_AUTOCONF_ENV([CC CXX CFLAGS CXXFLAGS CPPFLAGS LIBS MAKE GMAKE AR RANLIB REXEC AWK])
AC_PROG_AWK
GASNET_GET_AUTOCONF_VERSION()

########################################################################
##
##  Site-Specific Prefabricated Collections of Settings
##

## Berkeley Millennium cluster

#AC_MSG_CHECKING(for Berkeley Millennium cluster)
#AC_ARG_ENABLE(millennium,
#GASNET_OPTION_HELP(enable-millennium, use known directories for Berkeley Millennium cluster),
#,[case "`hostname`" in
#  *.Millennium.Berkeley.EDU) enable_millennium=yes ;;
#  *) enable_millennium=no ;;
#esac])
#if test "$enable_millennium" = yes; then
#fi
#AC_MSG_RESULT($enable_millennium)


########################################################################
##
##  Supporting Command-Line Tools
##

dnl     Store full paths, so we find right ones even if users have
dnl     some other version in their path
GASNET_PATH_PROGS(GMAKE, $GMAKE gmake make $MAKE, GNU make)
AC_MSG_CHECKING(for GNU make)
if $GMAKE --version | grep GNU ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_ERROR(cannot find a version of GNU make - please install GNU make and/or set \$GMAKE to indicate where it's located)
fi
AC_SUBST(GMAKE)



########################################################################
##
##  C/C++ Compilers
##

AC_PROG_CC
AC_PROG_CPP
AC_PROG_LN_S
AC_C_BIGENDIAN
AC_CHECK_PROG(have_mpcc_r,mpcc_r,yes,no)

AC_MSG_CHECKING(known buggy C compilers)
AC_TRY_CPP([
#if __GNUC__ == 2 && __GNUC_MINOR__ == 96 && __GNUC_PATCHLEVEL__ == 0
# error
#endif
],[ AC_MSG_RESULT(ok) ],[
AC_MSG_RESULT(gcc 2.96)
gcc296msg="Use of gcc 2.96 for compiling this software is strongly discouraged. \
It is not an official GNU release and has many serious known bugs, especially \
in the optimizer, which may lead to bad code and incorrect runtime behavior. \
Consider using \$CC to select a different compiler."
GASNET_IF_ENABLED(allow-gcc296, Allow the use of the broken gcc 2.96 compiler, [ 
  AC_MSG_WARN([$gcc296msg])  
  ],[ 
  AC_MSG_ERROR([$gcc296msg \
  You may enable use of this broken compiler at your own risk by passing the --enable-allow-gcc296 flag.])  
])
])

AC_SUBST(CC)

## DOB: this is more trouble than it's worth because so many OS's define
## LITTLE_ENDIAN or BIG_ENDIAN in random and sultry header files
## Code with access to gasnet_config.h should use #if defined(WORDS_BIGENDIAN)
## If we ever decide we need this (to support other code) then we 
## can reintroduce this define with a fresh prefix
dnl if test "$ac_cv_c_bigendian" = 'yes'; then
dnl   ENDIANNESS="-DBIG_ENDIAN"
dnl   # avoid warnings from re-defining BIG_ENDIAN
dnl   GASNET_TRY_CACHE_CHECK(for BIG_ENDIAN in sys/types.h, cc_bigend_systypes,
dnl     [#include <sys/types.h>
dnl      #ifndef BIG_ENDIAN
dnl        #error not defined
dnl      #endif
dnl     ], [], 
dnl     ENDIANNESS="")
dnl else
dnl   ENDIANNESS="-DLITTLE_ENDIAN=1234"
dnl   # avoid warnings from re-defining LITTLE_ENDIAN
dnl   GASNET_TRY_CACHE_CHECK(for LITTLE_ENDIAN in stdlib.h, cc_littleend_stdlib,
dnl     [#include <stdlib.h>
dnl      #ifndef LITTLE_ENDIAN
dnl        #error not defined
dnl      #endif
dnl     ], [], 
dnl     ENDIANNESS="")
dnl fi
dnl AC_SUBST(ENDIANNESS)

## specific compiler families

GASNET_FAMILY_CACHE_CHECK(C, CC, gasnet_cv_cc_family)

if test "$gasnet_cv_cc_family" = MIPS; then
  # The MIPSPro C compiler has a broken C preprocessor exit code, so we hardwire some choices
  AC_DEFINE(MIPSPRO_COMPILER)
fi

### 
#
# choose the default CC flags
#

case "$gasnet_cv_cc_family" in
  GNU)  oldCFLAGS="$CFLAGS"
        CFLAGS="-O3"
        # -finline-limit not available on gcc 2.95
        GASNET_TRY_CFLAG([-finline-limit=10000],
                         [CFLAGS="$CFLAGS -finline-limit=10000"])
        # We'd like to use -Winline to detect call sites where the optimizer
        # ignores our inline function modifier (usually due to technical limitations)
  	# However, some versions of gcc issue spurious warnings with -Winline
        # for inlining operations which the user did not request.
        # Detect that gcc bug and avoid -Winline for those versions
        GASNET_TRY_CFLAG([-Winline],[
          oldCFLAGS2="$CFLAGS"
          CFLAGS="$CFLAGS -Winline"
          AC_MSG_CHECKING(for buggy -Winline)
          GASNET_TRY_CCOMPILE_WITHWARN([
           int foo() { return 0; }
           int bar() { return foo() + bar(); }
          ], [ 
           int x = bar(); 
          ], [ AC_MSG_RESULT(ok) # keep -Winline
          ], [ AC_MSG_RESULT(buggy)
               CFLAGS="$oldCFLAGS2" # remove -Winline
          ], [ AC_MSG_ERROR(failure when detecting buggy -Winline)])
        ])

        CCOPTFLAGS="$CFLAGS"
        CFLAGS="$oldCFLAGS"
  ;;
  Sun)  CCOPTFLAGS="-xO5" ;;
  Cray) CCOPTFLAGS="-O2" ;; # DOB: -O3 is unstable on Cray cc
  MIPS) CCOPTFLAGS="-O3" ;;
  XLC)  #CCOPTFLAGS="-O5 -qsmp=noauto -qmaxmem=8192" ;; # -O5 == -O3 w/ intra-procedural analysis
        CCOPTFLAGS="-O3 -qsmp=noauto -qmaxmem=8192" ;; # -O5 causes linker to hang on Titanium
  Compaq) CCOPTFLAGS="-fast -noifo -O4 -tune host -inline all" ;;
  Intel) CCOPTFLAGS="-O3" ;;
  PGI) CCOPTFLAGS="-O2" ;;
  *)    CCOPTFLAGS="-O" ;;
esac

dnl GASNET_GET_GNUWARNINGFLAGS(type)  type=C or CXX
AC_DEFUN([GASNET_GET_GNUWARNINGFLAGS],[
        [$1]FLAGS="-g3"
        ]GASNET_TRY_[$1]FLAG[([-Wall],[[$1]FLAGS="$[$1]FLAGS -Wall"])
        ]GASNET_TRY_[$1]FLAG[([-Wno-unused],[[$1]FLAGS="$[$1]FLAGS -Wno-unused"])
        ]GASNET_TRY_[$1]FLAG[([-Wpointer-arith],[[$1]FLAGS="$[$1]FLAGS -Wpointer-arith"])
        ]GASNET_TRY_[$1]FLAG[([-Wnested-externs],[[$1]FLAGS="$[$1]FLAGS -Wnested-externs"])
        ]GASNET_TRY_[$1]FLAG[([-Wwrite-strings],[[$1]FLAGS="$[$1]FLAGS -Wwrite-strings"])
        ]GASNET_TRY_[$1]FLAG[([-Wmissing-format-attribute],[[$1]FLAGS="$[$1]FLAGS -Wmissing-format-attribute"])
        # some crappy pthread mutex implementations generate warnings without -Wno-missing-braces
	AC_MSG_CHECKING(for buggy pthread.h mutex initializers)
        old[$1]FLAGS="$[$1]FLAGS"
        # OSF and FREEBSD require the -pthread compiler flag when including pthread.h
        case "$target_os" in 
          osf*)     [$1]FLAGS="-pthread $[$1]FLAGS" ;;
          freebsd*) [$1]FLAGS="-pthread $[$1]FLAGS" ;;
        esac 
        ]GASNET_TRY_[$1]COMPILE_WITHWARN[([#include <pthread.h>],
          [pthread_mutex_t fastmutex = PTHREAD_MUTEX_INITIALIZER;],
          [AC_MSG_RESULT(ok)], [AC_MSG_RESULT(buggy)
          ]GASNET_TRY_[$1]FLAG[([-Wno-missing-braces],[old[$1]FLAGS="$old[$1]FLAGS -Wno-missing-braces"])],
          [AC_MSG_ERROR(failure while checking for buggy pthread.h mutexes)]
        )
        [$1]FLAGS="$old[$1]FLAGS"
        case "$target_os" in 
          solaris*) # hide pragma warnings in system header files included by absolute path
            ]GASNET_TRY_[$1]FLAG[([-Wno-unknown-pragmas],[[$1]FLAGS="$[$1]FLAGS -Wno-unknown-pragmas"])
        esac
        #]GASNET_TRY_[$1]FLAG[([-ansi -U__STRICT_ANSI__],[[$1]FLAGS="$[$1]FLAGS -ansi -U__STRICT_ANSI__"])
])

case "$gasnet_cv_cc_family" in
  GNU)  oldCFLAGS="$CFLAGS"
        GASNET_GET_GNUWARNINGFLAGS(C)
        CCDEBUGFLAGS="$CFLAGS"
        CFLAGS="$oldCFLAGS"
  ;;
  HP)   CCDEBUGFLAGS="-g +ESdbgasm" ;; # need +ESdbgasm to use -g with _asm()
  *)    CCDEBUGFLAGS="-g" ;;
esac

case "$gasnet_cv_cc_family" in
  Cray) MISC_CFLAGS="-hnomessage=236 -htaskprivate $LIBCM" ;; 
  MIPS) MISC_CFLAGS="-common -diag_suppress1171,1174,1209,1552 -LD_MSG:off=84" ;;
  Compaq) MISC_CFLAGS="-msg_disable extrasemi" ;;
  Intel) MISC_CFLAGS="-wd279" ;;
  PGI) MISC_CFLAGS="-Masmkeyword" ;;
  Sun) MISC_CFLAGS="-errtags" ;; # show warning tag ids in warnings
  *)    MISC_CFLAGS="" ;;
esac

case "$gasnet_cv_cc_family" in
  XLC) MISC_CPPFLAGS="-qcpluscmt" ;; # tell preprocessor to allow C++-style comments
  Sun) MISC_CPPFLAGS="-xCC" ;; # C++ comments
  *)    MISC_CPPFLAGS="" ;;
esac

case "$target_os" in
    darwin*) MISC_CPPFLAGS="-no-cpp-precomp -Wno-long-double $MISC_CPPFLAGS" ;; 
esac

GASNET_IF_ENABLED(debug, Build GASNet in a debugging mode, 
  [ CFLAGS="$CCDEBUGFLAGS"
    BUILDCONFIG_DEFINES="-DGASNET_DEBUG"
    BUILDCONFIG="debug"
    enabled_debug=yes],
  [ CFLAGS="$CCOPTFLAGS"
    BUILDCONFIG_DEFINES="-DGASNET_NDEBUG"
    BUILDCONFIG="optimize"
    enabled_debug=no])

GASNET_IF_ENABLED(gasnet-verbose, Build GASNet lib with debugging status messages, 
  [ BUILDCONFIG_DEFINES="$BUILDCONFIG_DEFINES -DGASNET_DEBUG_VERBOSE=1"], [])

GASNET_IF_ENABLED_WITH_AUTO(trace, Build GASNet with tracing enabled (enabled by default with --enable-debug), 
  enabled_trace=yes, enabled_trace=no, enabled_trace=$enabled_debug)
  if test "$enabled_trace" = yes; then
    BUILDCONFIG_DEFINES="$BUILDCONFIG_DEFINES -DGASNET_TRACE"
  fi

GASNET_IF_ENABLED_WITH_AUTO(stats, Build GASNet with statistical collection enabled (enabled by default with --enable-debug), 
  enabled_stats=yes, enabled_stats=no, enabled_stats=$enabled_debug)
  if test "$enabled_stats" = yes; then
    BUILDCONFIG_DEFINES="$BUILDCONFIG_DEFINES -DGASNET_STATS"
  fi

AC_SUBST(CFLAGS)	      # opt/debug flags
AC_SUBST(MISC_CFLAGS)	      # warning suppression and other misc C flags
AC_SUBST(MISC_CPPFLAGS)	      # misc preprocessor flags
AC_SUBST(BUILDCONFIG_DEFINES) # defines that setup build configuration

segconfig=""
GASNET_IF_ENABLED(segment-fast, Build GASNet in the FAST shared segment configuration,
  segconfig="fast$segconfig", )
GASNET_IF_ENABLED(segment-large, Build GASNet in the LARGE shared segment configuration,
  segconfig="large$segconfig", )
GASNET_IF_ENABLED(segment-everything, Build GASNet in the EVERYTHING shared segment configuration,
  segconfig="everything$segconfig", )

case "$segconfig" in
  "") segconfig=fast; #for AM_CONDITIONAL, below
      AC_DEFINE(GASNET_SEGMENT_FAST) ;;  # default is fast
  fast) AC_DEFINE(GASNET_SEGMENT_FAST) ;;
  large) AC_DEFINE(GASNET_SEGMENT_LARGE) ;;
  everything) AC_DEFINE(GASNET_SEGMENT_EVERYTHING) ;;
  *) AC_MSG_ERROR(Conflicting shared segment configurations specified) ;;
esac

# Do it all again because AM_CONDITIONAL()s can't go inside if or case.
# Groan.
AM_CONDITIONAL(GASNET_SEGMENT_FAST, test "$segconfig" = fast)
AM_CONDITIONAL(GASNET_SEGMENT_LARGE, test "$segconfig" = large)
AM_CONDITIONAL(GASNET_SEGMENT_EVERYTHING, test "$segconfig" = everything)


GASNET_LIBGCC

########################################################################
# hunt for 16-, 32-, and 64-bit integer types
# Check sizes
AC_CHECK_SIZEOF(char, $cross_char)
AC_CHECK_SIZEOF(short, $cross_short)
AC_CHECK_SIZEOF(int, $cross_int)
AC_CHECK_SIZEOF(long, $cross_long)
AC_CHECK_SIZEOF(long long, $cross_long_long)
AC_CHECK_SIZEOF(void *, $cross_void_P)

AM_CONDITIONAL(PLATFORM_ILP32, test x"$ac_cv_sizeof_int$ac_cv_sizeof_long$ac_cv_sizeof_void_p" = x444)
AM_CONDITIONAL(PLATFORM_LP64, test x"$ac_cv_sizeof_int$ac_cv_sizeof_long$ac_cv_sizeof_void_p" = x488)
AM_CONDITIONAL(PLATFORM_ILP64, test x"$ac_cv_sizeof_int$ac_cv_sizeof_long$ac_cv_sizeof_void_p" = x888)

AC_CHECK_HEADERS(inttypes.h)

########################################################################
##
##  Page size
##

if test "$GASNETI_PAGESIZE" = ""; then
GASNET_TRY_CACHE_RUN_EXPR(PAGESIZE, PAGESIZE,
  [ #include <limits.h> ], [ (PAGESIZE>0?PAGESIZE:(exit(1),0)) ], GASNETI_PAGESIZE)
fi

if test "$GASNETI_PAGESIZE" = ""; then
GASNET_TRY_CACHE_RUN_EXPR(PAGE_SIZE, PAGE_SIZE,
  [ #include <limits.h> ], [ (PAGE_SIZE>0?PAGE_SIZE:(exit(1),0)) ], GASNETI_PAGESIZE)
fi

if test "$GASNETI_PAGESIZE" = ""; then
GASNET_TRY_CACHE_RUN_EXPR([sysconf(_SC_PAGESIZE)], _SC_PAGESIZE,
  [ #include <unistd.h> ], [ (sysconf(_SC_PAGESIZE)>0?sysconf(_SC_PAGESIZE):(exit(1),0)) ], GASNETI_PAGESIZE)
fi

if test "$GASNETI_PAGESIZE" = ""; then
GASNET_TRY_CACHE_RUN_EXPR([sysconf(_SC_PAGE_SIZE)], _SC_PAGE_SIZE,
  [ #include <unistd.h> ], [ (sysconf(_SC_PAGE_SIZE)>0?sysconf(_SC_PAGE_SIZE):(exit(1),0)) ], GASNETI_PAGESIZE)
fi

if test "$GASNETI_PAGESIZE" = ""; then
GASNET_TRY_CACHE_RUN_EXPR([getpagesize()], getpagesize,
  [ #include <unistd.h> ], [ (getpagesize()>0?getpagesize():(exit(1),0)) ], GASNETI_PAGESIZE)
fi

if test "$GASNETI_PAGESIZE" != ""; then
  AC_DEFINE_UNQUOTED(GASNETI_PAGESIZE,$GASNETI_PAGESIZE)

  case "$GASNETI_PAGESIZE" in
    1024)   GASNETI_PAGESHIFT=10 ;;
    2048)   GASNETI_PAGESHIFT=11 ;;
    4096)   GASNETI_PAGESHIFT=12 ;;
    8192)   GASNETI_PAGESHIFT=13 ;;
    16384)  GASNETI_PAGESHIFT=14 ;;
    32768)  GASNETI_PAGESHIFT=15 ;;
    65536)  GASNETI_PAGESHIFT=16 ;;
    131072) GASNETI_PAGESHIFT=17 ;;
    *) AC_MSG_ERROR(unknown GASNETI_PAGESIZE=$GASNETI_PAGESIZE) ;;
  esac
  AC_DEFINE_UNQUOTED(GASNETI_PAGESHIFT,$GASNETI_PAGESHIFT)
fi

########################################################################
##
##  Cluster computing libraries
##


GASNET_CHECK_LIB(ens, NameServer_Init, have_libens=yes, , -L/usr/lib ${with_ens:+-L$with_ens})
AC_CHECK_LIB(glunix, Glib_Initialize, have_libglunix=yes, , -lsocket)

AC_PATH_PROG(REXEC, $REXEC rexec, , $with_rexec:$PATH)

AC_MSG_CHECKING(for Millennium rexec)
# We're only interested in the Millennium version of rexec
# the standard UNIX command 'rexec' has completely different behavior and is currently of no use to us
if test -n "$REXEC" -a -n "`$REXEC 2> /dev/null | grep -- '-n #nodes'`"; then
  have_rexec=yes
  GASNET_REXEC_DEFINES="-DREXEC"
  AC_MSG_RESULT(yes)
else
  have_rexec=no
  GASNET_REXEC_DEFINES=""
  AC_MSG_RESULT(no)
fi

#---------------------------------------------------------------------------------------------------------------
# Machine defs

AC_DEFINE(UNIX)
GASNET_THREAD_DEFINES=""
GASNET_THREAD_LIBS=""
case "$target_os" in
  solaris*)  
	AC_DEFINE(SOLARIS)
	GASNET_MACHINE_DEFINES="-DSOLARIS" 
	GASNET_THREAD_DEFINES="-D_REENTRANT"
        # on some systems static linking fails with -lpthread, 
        # but need it or pthread_create will fail at runtime
        GASNET_THREAD_LIBS="-lpthread" 
        ;;
  linux*) 
	AC_DEFINE(LINUX)
	GASNET_MACHINE_DEFINES="-DLINUX"
        # Defining _GNU_SOURCE gives us accesss to the "adaptive"
        # implementation of pthread_mutex_t, which is faster then
        # the default implementation for our purposes.
	GASNET_THREAD_DEFINES="-D_REENTRANT -D_GNU_SOURCE"
        GASNET_THREAD_LIBS="-lpthread"
	;;
  freebsd*) 
	AC_DEFINE(FREEBSD)
	GASNET_MACHINE_DEFINES="-DFREEBSD"
	GASNET_THREAD_DEFINES="-D_REENTRANT"
        GASNET_THREAD_LIBS="-pthread -lc_r"
	;;
  cygwin*) 
	AC_DEFINE(CYGWIN)
	GASNET_MACHINE_DEFINES="-DCYGWIN"
	GASNET_THREAD_DEFINES="-D_REENTRANT"
        GASNET_THREAD_LIBS=""
	;;
  darwin*) 
	AC_DEFINE(DARWIN)
	GASNET_MACHINE_DEFINES="-DDARWIN"
	GASNET_THREAD_DEFINES="-D_REENTRANT"
        GASNET_THREAD_LIBS=""
	;;
  irix*)
	AC_DEFINE(IRIX)
	GASNET_MACHINE_DEFINES="-DIRIX"
	GASNET_THREAD_DEFINES="-D_REENTRANT"
	GASNET_THREAD_LIBS="-lpthread"
        ;;
  aix*)
	AC_DEFINE(AIX)
        GASNET_MACHINE_DEFINES="-DAIX"
        GASNET_THREAD_DEFINES="-D_REENTRANT -D_THREAD_SAFE"
        GASNET_THREAD_LIBS="-lpthread"
	;;
  osf*)
	AC_DEFINE(OSF)
	GASNET_MACHINE_DEFINES="-DOSF"
        GASNET_THREAD_DEFINES="-D_REENTRANT -D_THREAD_SAFE"
        GASNET_THREAD_LIBS="-pthread -lpthreads"
	;;
  hpux*)
	AC_DEFINE(HPUX)
	GASNET_MACHINE_DEFINES="-DHPUX"
        GASNET_THREAD_DEFINES="-D_REENTRANT"
        GASNET_THREAD_LIBS="-lpthread"
	;;
  unicos*)
	AC_DEFINE(UNICOS)
        GASNET_MACHINE_DEFINES="-DUNICOS"
        GASNET_THREAD_DEFINES="-D_REENTRANT"
        GASNET_THREAD_LIBS="-lpthread"
	GASNET_TRY_CACHE_CHECK(for Cray T3E, cc_is_t3e, [], [
	    #ifndef _CRAYT3E
	      #error not Cray T3E
	    #endif
	  ], [
	    GASNET_MACHINE_DEFINES="$GASNET_MACHINE_DEFINES -DCRAYT3E"
	    AC_DEFINE(CRAYT3E)
	  ])
        ;;
  *)
        AC_MSG_WARN(unknown OS - you may need to update the configure script)
        ;;
esac

GASNET_MACHINE_DEFINES="$GASNET_MACHINE_DEFINES -DUNIX"

AC_SUBST(GASNET_MACHINE_DEFINES)
AC_SUBST(GASNET_THREAD_DEFINES)
AC_SUBST(GASNET_THREAD_LIBS)
AC_SUBST(GASNET_REXEC_DEFINES)

# Find the appropriate OS suffix for executables
case "$target_os" in
  cygwin*)    EXESUFFIX='.exe' ;;
  *)          EXESUFFIX='' ;;
esac
AC_SUBST(EXESUFFIX)

########################################################################
##
##  Multithreading Libraries
##


## POSIX threads
#             
OLDLIBS="$LIBS"   
LIBS="$LIBS $GASNET_THREAD_LIBS"
AC_SEARCH_LIBS(pthread_create, [pthread pthreads c_r], have_pthread=yes, have_pthread=no)
case "$target_os" in
  hpux*) have_pthread=yes ;; # test fails on HPUX due to signature mismatch
esac        
LIBS="$OLDLIBS"

if test "$have_pthread" = no ; then
  GASNET_THREAD_DEFINES=""
  GASNET_THREAD_LIBS=""
  AC_MSG_WARN(No pthreads support found - disabling pthreads)
fi
          
AM_CONDITIONAL(HAVE_PTHREAD, test "$have_pthread" = yes)

## Solaris threads

AC_CHECK_LIB(thread, thr_create, have_solthread=yes, have_solthread=no)

#---------------------------------------------------------------------------------------------------------------
# Check for overrides of compile-time checks for SMPs
GASNET_IF_ENABLED_WITH_AUTO(smp-safe, Enable build of SMP-safe libraries (default is to probe at configure time),
  [ uni_build=no ],
  [ uni_build=yes],
  [ AC_CHECK_HEADERS(/boot/kernel.h)
    AC_MSG_CHECKING(whether current machine is a uni-processor)
    if test "`uname -a | grep -i SMP`" != ""; then
      uni_build=no;
    else
      AC_TRY_COMPILE([
        /* Note that we try to error on the side of an SMP */
        #if defined(__linux__)
          #include <linux/config.h>
          #if defined(CONFIG_SMP)
            #error Make the AC_TRY_COMPILE fail (SMP)
          #endif
          #ifdef HAVE__BOOT_KERNEL_H
            #include </boot/kernel.h>
            #if defined(__BOOT_KERNEL_SMP) && (__BOOT_KERNEL_SMP == 1)
              #error Make the AC_TRY_COMPILE fail (SMP)
            #endif
          #endif
        #elif defined(__FreeBSD__)
          #if defined(SMP)
            #error Make the AC_TRY_COMPILE fail (SMP)
          #endif
        #else
          #error Make the AC_TRY_COMPILE fail (SMP)
        #endif
    ], [], [ uni_build=yes ], [ uni_build=no ])
    fi
    AC_MSG_RESULT($uni_build)
  ] 
 )
if test "$uni_build" = yes; then
  AC_DEFINE(GASNETI_UNI_BUILD)
fi

#---------------------------------------------------------------------------------------------------------------
# Hack-around to prevent a bogus autoconf 2.58 bug when AC_PROG_CXX is called conditionally below
am__fastdepCXX_TRUE='#'
am__fastdepCXX_FALSE=
# Configure AMUDP
GASNET_IF_ENABLED_WITH_AUTO(udp, Enable the portable UDP network conduit (on by default),
  [enabled_udp=yes;force_udp=yes],
  enabled_udp=no,
  enabled_udp=yes)
if test $enabled_udp = yes; then

  # Do C++ configuration
  AC_LANG_SAVE

  AC_PROG_CXX
  AC_PROG_CXXCPP

  AC_LANG_CPLUSPLUS
  GASNET_FAMILY_CACHE_CHECK(C++, CXX, gasnet_cv_cxx_family)
  CXXDEBUGFLAGS="-g"
  CXXOPTFLAGS="-O2"
  case "$gasnet_cv_cxx_family" in
    GNU) 
       oldCXXFLAGS="$CXXFLAGS"  
       GASNET_GET_GNUWARNINGFLAGS(CXX)
       MISC_CXXFLAGS="$CXXFLAGS" 
       CXXFLAGS="$oldCXXFLAGS"
    ;;
    PGI)   MISC_CXXFLAGS="-w -Masmkeyword" ;; # apparently has no way to indep control warnings
    KAI)   MISC_CXXFLAGS="--diag_suppress 611 --diag_suppress 610" ;;
    MIPS)  MISC_CXXFLAGS="-diag_suppress 1171,1174,1209,1552,1681,1682 -LANG:std -common" ;;
    Intel) MISC_CXXFLAGS="-wd654 -wd1125 -wd279" ;;
    Cray)  # Cray CC v3.3.0.2 fails to accept "-htaskprivate", but accepts "-htask private"
     MISC_CXXFLAGS="-hnomessage=236:611:997 -hexceptions -hnew_for_init -htask private $LIBCM" ;;
  esac  
  if test "$BUILDCONFIG" = "optimize" ; then
    CXXFLAGS="$CXXOPTFLAGS"
  else
    CXXFLAGS="$CXXDEBUGFLAGS"
  fi
  AC_SUBST(CXX)
  AC_SUBST(CXXFLAGS)
  AC_SUBST(MISC_CXXFLAGS)

  AC_LANG_RESTORE

  supported_amudp=yes 
  if test "$force_udp$supported_udp" = yesno ; then
    AC_MSG_ERROR(User requested --enable-udp but AMUDP is not supported on your system)
  fi

  if test $supported_amudp = yes; then
    # detect libraries necessary for udp-* backends
    OLDLIBS="$LIBS"
      LIBS=""
      AC_SEARCH_LIBS(socket, socket, have_udp=yes, have_udp=no)
      AC_SEARCH_LIBS(gethostbyname, nsl, , have_udp=no)
      GASNET_UDP_LIBS="$LIBS"

      if test "$have_udp" = yes ; then
	AC_MSG_CHECKING(for working UDP configuration)
	# TODO: add a basic test of the UDP configuration to make sure it works
	# this is a mess because every OS has different headers required
	udp_test_worked=yes
      
	if test "$udp_test_worked" = yes ; then
	    AC_MSG_RESULT(yes)
	else
	    AC_MSG_RESULT(no)
	    AC_MSG_WARN(build test failed: I don't know how to build UDP programs on your system)
	    have_udp=no
	fi
      fi

      if test "$force_udp$have_udp" = yesno ; then
	AC_MSG_ERROR(User requested --enable-udp but I don't know how to build UDP programs for your system)
      fi

    LIBS="$OLDLIBS"
  fi
fi
AC_SUBST(GASNET_UDP_LIBS)
AM_CONDITIONAL(USE_UDP_CONDUIT, test "$enabled_udp$have_udp" = yesyes)

GASNET_ENV_DEFAULT(GASNET_CSPAWN_CMD, "$TI_CSPAWN_CMD")
if test "$GASNET_CSPAWN_CMD" != "" ; then
  AC_DEFINE_UNQUOTED(GASNET_CSPAWN_CMD, "$GASNET_CSPAWN_CMD")
fi

#---------------------------------------------------------------------------------------------------------------
# Configure AMMPI 
# (In the future it would be nice to try and discover MPI setup automatically)
GASNET_IF_ENABLED_WITH_AUTO(mpi, Enable the portable MPI network conduit (on by default), 
  [enabled_mpi=yes;force_mpi=yes],
  enabled_mpi=no,
  enabled_mpi=yes)
if test $enabled_mpi = yes; then
  # user can explicitly configure MPI by setting MPI_CC, MPI_CFLAGS and MPI_LIBS
  have_mpi=yes # start by assuming they have it
  case "$target_os" in
    solaris*)  
        if test "$enable_now" = "yes" ; then
          # this config is for AMMPI-over-MPI-over-AMNOW (Berkeley NOW cluster)
    	  # (not meant as a production platform)
          # Use Sun cc (man acc to see options)
    	  GASNET_ENV_DEFAULT(MPI_CC, /usr/sww/opt/SUNWspro/bin/cc)
	  BASIC_MPI_CFLAGS="-DAMMPI_COEXIST_WITH_AM \
		-I/usr/now/am2/include -I/usr/now/include -I/usr/now/mpi/mpich/include \
		-w"
	  if test "$BUILDCONFIG" = "optimize" ; then
    	    GASNET_ENV_DEFAULT(MPI_CFLAGS,  $BASIC_MPI_CFLAGS \
		-dalign -xarch=v8plusa -xchip=ultra -fast -xtarget=native -xO5)
	  else
    	    GASNET_ENV_DEFAULT(MPI_CFLAGS, $BASIC_MPI_CFLAGS -g -w)
	  fi
    	  GASNET_ENV_DEFAULT(MPI_LIBS, -lposix4 \
              -L/usr/now/mpi/mpich/lib/solaris/am2 -L/usr/now/lib\
              -L/usr/now/am2/lib -lmpi -lens -lglunix -lam2 -lthread\
              -lsocket -lnsl)
	  GASNET_ENV_DEFAULT(MPIRUN_CMD, glurun -%N %C)
	  CFLAGS="$CFLAGS -DAMMPI_COEXIST_WITH_AM"
        else
          # I don't have access to a Solaris cluster with MPI (other than NOW) 
          # but these seem like sane defaults
    	  GASNET_ENV_DEFAULT(MPI_CC, mpicc)
    	  GASNET_ENV_DEFAULT(MPI_CFLAGS, $MISC_CFLAGS $CFLAGS)
    	  GASNET_ENV_DEFAULT(MPI_LIBS, )
	  GASNET_ENV_DEFAULT(MPIRUN_CMD, mpirun -np %N %C)
        fi
        ;;
    linux*) 
    	GASNET_ENV_DEFAULT(MPI_CFLAGS, $CFLAGS)
    	GASNET_ENV_DEFAULT(MPI_LIBS, )
        if test "$enable_millennium" = yes; then

          #mpi_loc=/usr/mill/bin  # TCP/IP implementation
          # Millennium has screwy mpirun syntax to assure distinct nodes on TCP/IP p4 device
	  #GASNET_ENV_DEFAULT(MPIRUN_CMD, ${mpi_loc}/mpirun -np %N -nodes %N %C)

          mpi_loc=/usr/mill/pkg/mpich-1.2.1..7-gm/bin  # GM-based implementation
	  GASNET_ENV_DEFAULT(MPIRUN_CMD, ${mpi_loc}/mpirun -np %N %C)

    	  GASNET_ENV_DEFAULT(MPI_CC, ${mpi_loc}/mpicc)
        else
    	  GASNET_ENV_DEFAULT(MPI_CC, mpicc)
	  GASNET_ENV_DEFAULT(MPIRUN_CMD, mpirun -np %N %C)
        fi
        ;;
    irix*)
	# use SGI native MPI library
        # user may need to do a "module load mpt" to make this work
    	GASNET_ENV_DEFAULT(MPI_CC, cc -n32)
        # cannot use MISC_CFLAGS here because C compiler may not be MIPSPro
	BASIC_MPI_CFLAGS="-diag_suppress1171,1174,1209,1552"
	if test "$BUILDCONFIG" = "optimize" ; then
      	  GASNET_ENV_DEFAULT(MPI_CFLAGS, -O2 $BASIC_MPI_CFLAGS)
	else
      	  GASNET_ENV_DEFAULT(MPI_CFLAGS, -g $BASIC_MPI_CFLAGS)
	fi
    	GASNET_ENV_DEFAULT(MPI_LIBS, -LD_MSG:off=84 -lmpi)
	GASNET_ENV_DEFAULT(MPIRUN_CMD, mpirun -np %N %C)
        ;;
    aix*)
    	GASNET_ENV_DEFAULT(MPI_CC, mpcc)
	BASIC_MPI_CFLAGS=""
	if test "$BUILDCONFIG" = "optimize" ; then
    	  GASNET_ENV_DEFAULT(MPI_CFLAGS, -O3 -qstrict -qarch=pwr3 -qtune=pwr3 -qsmp=noauto -qmaxmem=8192 $BASIC_MPI_CFLAGS)
	else
    	  GASNET_ENV_DEFAULT(MPI_CFLAGS, -g $BASIC_MPI_CFLAGS)
	fi
    	GASNET_ENV_DEFAULT(MPI_LIBS, [-Wl,-bbigtoc])
	GASNET_ENV_DEFAULT(MPIRUN_CMD, poe %C -nodes %N -tasks_per_node 1 -rmpool 1 -euilib us -retry 1 -retrycount 10000)
        ;;
    osf*)
	GASNET_ENV_DEFAULT(MPI_CC, cc)
	BASIC_MPI_CFLAGS="-msg_disable extrasemi"
        if test "$BUILDCONFIG" = "optimize" ; then
          GASNET_ENV_DEFAULT(MPI_CFLAGS, -fast -noifo -O4 -tune host $BASIC_MPI_CFLAGS)
        else
          GASNET_ENV_DEFAULT(MPI_CFLAGS, -g $BASIC_MPI_CFLAGS)
        fi
        GASNET_ENV_DEFAULT(MPI_LIBS, -lmpi -lelan -lrt)
        GASNET_ENV_DEFAULT(MPIRUN_CMD, prun -N %N %C)
        ;;
    unicos*)
        # Cray cc - need to enable private global vars and disable a silly warning
        # user may need to do a "module load mpt" to make this work
    	GASNET_ENV_DEFAULT(MPI_CC, cc)
	BASIC_MPI_CFLAGS="-hnomessage=236 -htaskprivate $LIBCM"
	if test "$BUILDCONFIG" = "optimize" ; then
    	  GASNET_ENV_DEFAULT(MPI_CFLAGS, -O2 $BASIC_MPI_CFLAGS)
	else
    	  GASNET_ENV_DEFAULT(MPI_CFLAGS, -g $BASIC_MPI_CFLAGS)
	fi
    	GASNET_ENV_DEFAULT(MPI_LIBS, -lpthread)
	GASNET_ENV_DEFAULT(MPIRUN_CMD, mpprun -n %N %C)
        ;;
    *) 
      # unknown OS - if user doesn't provide info, nothing we can do
      GASNET_ENV_DEFAULT(MPI_CC, mpicc)
      GASNET_ENV_DEFAULT(MPI_CFLAGS, $MISC_CFLAGS $CFLAGS)
      GASNET_ENV_DEFAULT(MPI_LIBS, )
      GASNET_ENV_DEFAULT(MPIRUN_CMD, mpirun -np %N %C)
      if test "$MPI_CC" = ""; then
        AC_MSG_WARN(I don't know how to build MPI programs on your system)
        AC_MSG_WARN(consider using \$MPI_CC \$MPI_CFLAGS and \$MPI_LIBS to configure MPI support for your site if you want the mpi-* backends)
	MPI_CC=$CC
        have_mpi=no
      fi 
      ;;
  esac

  MPI_CFLAGS="$BUILDCONFIG_DEFINES $MPI_CFLAGS"

  # now run a basic test of the MPI configuration to make sure it works
  if test $have_mpi = yes; then
    AC_MSG_CHECKING(for working MPI configuration)
    # we need a clean slate here because MPI config may be totally different
    OLDCC="$CC"
    OLDCFLAGS="$CFLAGS"
    OLDCPPFLAGS="$CPPFLAGS"
    OLDLDFLAGS="$LDFLAGS"
    OLDLIBS="$LIBS"
    CC="$MPI_CC"
    CFLAGS="$MPI_CFLAGS"
    CPPFLAGS=""
    LDFLAGS=""
    LIBS="$MPI_LIBS"
    # we just try to link an MPI app - running MPI apps is a whole new can of worms
    AC_TRY_LINK( [
      #include <stdlib.h>
      #include <mpi.h>
    ], [
      MPI_Init(NULL,NULL);
      MPI_Finalize();
    ], [ mpi_test_worked=yes ], [ mpi_test_worked=no] )
    CC="$OLDCC"
    CFLAGS="$OLDCFLAGS"
    CPPFLAGS="$OLDCPPFLAGS"
    LDFLAGS="$OLDLDFLAGS"
    LIBS="$OLDLIBS"
  fi
  if test "$mpi_test_worked" = yes ; then
      AC_MSG_RESULT(yes)
      if echo "$MPI_CFLAGS" | grep -- "-DAMMPI_COEXIST_WITH_AM" > /dev/null ; then
        # -DAMMPI_COEXIST_WITH_AM must be included whenever ammpi.h is compiled, or not at all
        MPI_DEFINES="$MPI_DEFINES -DAMMPI_COEXIST_WITH_AM"
      fi
  else
      AC_MSG_RESULT(no)
      AC_MSG_WARN(build test failed: I don't know how to build MPI programs on your system)
      AC_MSG_WARN(consider using \$MPI_CC \$MPI_CFLAGS and \$MPI_LIBS to configure MPI support for your site if you want the mpi conduit)
      have_mpi=no
  fi
  if test "$force_mpi$have_mpi" = yesno ; then
    AC_MSG_ERROR(User requested --enable-mpi but I don't know how to build MPI programs for your system)
  fi
fi
AC_SUBST(MPI_CC) # compiler to be used to build AMMPI, and linker for AMMPI-based backends
AC_SUBST(MPI_CFLAGS) # compiler flags (optimization, includes, etc) to be used with above compiler when building AMMPI
AC_SUBST(MPI_LIBS) # libs and ldflags that must be used when linking AMMPI-based backend
AC_SUBST(MPI_DEFINES) # preprocessor defs to be used for all client code that includes ammpi.h (not including AMMPI library itself)
AC_SUBST(MPIRUN_CMD) # prototype command used by tcrun to run MPI programs
AM_CONDITIONAL(USE_MPI_CONDUIT, test "$enabled_mpi$have_mpi" = yesyes)
#--------------------------------------------------------------------------------------------------------

#-----------------------------------------------------------------------------------------------
# SMP configuration

GASNET_IF_ENABLED_WITH_AUTO(smp, Enable the SMP (loopback) network conduit (on by default),
  have_smp=yes,
  have_smp=no,
  have_smp=yes)
AM_CONDITIONAL(USE_SMP_CONDUIT, test "$have_smp" = yes)

#--------------------------------------------------------------------------------------------------------
# GM configuration

GASNET_IF_ENABLED(gm, Enable the Myricom/GM network conduit, 
  have_gm=yes,
  have_gm=no)
if test $have_gm = yes; then
  case "$target_os" in
    solaris*)
    ;;
    *)	# All unknown for now
        GASNET_ENV_DEFAULT(GM_INCLUDE, /usr/local/gm/include)
	GASNET_ENV_DEFAULT(GM_LIB, /usr/local/gm/lib)
	if test "$GM_INCLUDE" = "" || test ! -d "$GM_INCLUDE"; then
	  AC_MSG_ERROR(GM includes: make sure \$GM_INCLUDE is set and exists)
	  have_gm=no
	fi
	if test "$GM_LIB" = ""; then
	  AC_MSG_ERROR(GM library: make sure \$GM_LIB is set and exists)
	  have_gm=no
	fi
    ;;
  esac

  if test $have_gm = yes; then
    AC_MSG_CHECKING(for working GM configuration)
    OLDCFLAGS="$CFLAGS"
    OLDLIBS="$LIBS"
    CFLAGS="-I$GM_INCLUDE -L$GM_LIB"
    LIBS="-lgm"
    # try to compile and link a simple gm_init, gm_finalize
    AC_TRY_LINK( [
      #include <stdlib.h>
      #include <gm.h>
    ], [
      gm_init();
      gm_finalize();
    ], [ gm_test_worked=yes ], [ gm_test_worked=no ] )
    CFLAGS="$OLDCFLAGS"
    LIBS="$OLDLIBS"

    if test "$gm_test_worked" = yes; then
      AC_MSG_RESULT(yes)
    else
      AC_MSG_RESULT(no)
      AC_MSG_ERROR(build test failed: I don't know how to build GM programs on your system)
      have_gm=no
    fi
  fi
fi

AC_SUBST(GM_INCLUDE)
AC_SUBST(GM_LIB)
AM_CONDITIONAL(USE_GM_CONDUIT, test "$have_gm" = yes)

#--------------------------------------------------------------------------------------------------------
# LAPI configuration

GASNET_IF_ENABLED(lapi, Enable the LAPI (IBM SP) network conduit, 
  have_lapi=yes,
  have_lapi=no)

if test $have_lapi = yes; then
  AC_MSG_CHECKING(for working LAPI configuration)
  OLDLIBS="$LIBS"
  LIBS="-llapi_r -lpthread"
  # try to compile and link a simple LAPI program
  AC_TRY_LINK( [
    #include <lapi.h>
    #include <string.h>
  ], [{
      lapi_handle_t  gasnetc_lapi_context;
      lapi_info_t    gasnetc_lapi_info;

      memset(&gasnetc_lapi_info, 0, sizeof(lapi_info_t));
      LAPI_Init(&gasnetc_lapi_context, &gasnetc_lapi_info);
    }], [ lapi_test_worked=yes ], [ lapi_test_worked=no ] )
  LIBS="$OLDLIBS"

  if test "$lapi_test_worked" = yes; then
    AC_MSG_RESULT(yes)
  else
    AC_MSG_RESULT(no)
    AC_MSG_ERROR(build test failed: I don't know how to build lapi programs on your system)
    have_lapi=no
  fi

  if test x"$ac_cv_sizeof_void_p" = x4; then
    LAPI_LD="mpcc_r"
  else
    LAPI_LD="mpcc_r -q64"
  fi
  AC_SUBST(LAPI_LD)
fi

AM_CONDITIONAL(USE_LAPI_CONDUIT, test "$have_lapi" = yes)

#--------------------------------------------------------------------------------------------------------
# ELAN configuration

GASNET_IF_ENABLED(elan, Enable the elan (Quadrics) network conduit, 
  have_elan=yes,
  have_elan=no)

if test $have_elan = yes; then
  case "$target_os" in
    linux*)
	# the default locations are /usr/include and /usr/lib
        # rms libs are only there to support signalling exit, 
        # if they're problematic they can be removed provided 
        # you compile with -DGASNETC_USE_SIGNALING_EXIT=0
        GASNET_ENV_DEFAULT(ELAN_INCLUDE, /usr/include)
	GASNET_ENV_DEFAULT(ELAN_LIBS, -lelan -lelan3 -lrms -lrmscall -lrmsapi)
    ;;
    *)
	# the default locations are /usr/opt/rms/include and /usr/lib
        GASNET_ENV_DEFAULT(ELAN_INCLUDE, /usr/opt/rms/include)
	GASNET_ENV_DEFAULT(ELAN_LIBS, -lelan -lelan3 -lrms -lrmscall -lrmsapi)
    ;;
  esac

  ELAN_HEADERLOC="${ELAN_INCLUDE}/elan"
  # Don't actually place -I/usr/include on the compile line, because this
  # breaks IA64 Intel C, which relies on preceding /usr/include with its own
  # include directory that overrides some system headers (eg asm/*)
  if test `cd ${ELAN_INCLUDE}; pwd` = "/usr/include"; then
    ELAN_INCLUDE=.
  fi

  AC_MSG_CHECKING(for working ELAN configuration)
  OLDCFLAGS="$CFLAGS"
  OLDLIBS="$LIBS"
  CFLAGS="-I$ELAN_INCLUDE"
  LIBS="$ELAN_LIBS"
  # try to compile and link a simple elan prog
  AC_TRY_LINK( [
    #include <stdlib.h>
    #include <elan/elan.h>
  ], [
   #ifdef QSNETLIBS_VERSION_CODE
     #if QSNETLIBS_VERSION_CODE >= QSNETLIBS_VERSION(1,4,10)
       elan_baseInit(0);
     #else
       elan_baseInit();
     #endif
   #else
     elan_baseInit();
   #endif
  ], [ elan_test_worked=yes ], [ elan_test_worked=no ] )
  CFLAGS="$OLDCFLAGS"
  LIBS="$OLDLIBS"

  if test "$elan_test_worked" = yes; then
    AC_MSG_RESULT(yes)
    AC_MSG_CHECKING(for ELAN version)
    if test -e ${ELAN_HEADERLOC}/version.h; then
      verstr=`grep QSNETLIBS_VERSION_STRING ${ELAN_HEADERLOC}/version.h`
      [majorver=`echo $verstr | $AWK -F'[ \t]*|\"|[.]' '{ print $4 }'`]
      [minorver=`echo $verstr | $AWK -F'[ \t]*|\"|[.]' '{ print $5 }'`]
      [subver=`echo $verstr | $AWK -F'[ \t]*|\"|[.]' '{ print $6 }'`]
    else
      verstr=`grep 'ELAN_VERSION' ${ELAN_HEADERLOC}/misc.h`
      [majorver=`echo $verstr | $AWK -F'[ \t]*|\"|[.]' '{ print $5 }'`]
      [minorver=`echo $verstr | $AWK -F'[ \t]*|\"|[.]' '{ print $6 }'`]
      subver=0
    fi
    AC_MSG_RESULT("${majorver}.${minorver}.${subver}")
    if test "$majorver" = "" -o "$minorver" = "" -o "$subver" = "" ; then
      AC_MSG_ERROR("unable to determine libelan version")
    fi
    ELAN_FLAGS="-DELAN_VERSION_MAJOR=${majorver} -DELAN_VERSION_MINOR=${minorver} -DELAN_VERSION_SUB=${subver}"
  else
    AC_MSG_RESULT(no)
    ELAN_FLAGS=""
    AC_MSG_ERROR(build test failed: I don't know how to build ELAN programs on your system)
  fi
fi

AC_SUBST(ELAN_INCLUDE)
AC_SUBST(ELAN_LIBS)
AC_SUBST(ELAN_FLAGS)
AM_CONDITIONAL(USE_ELAN_CONDUIT, test "$have_elan" = yes)

#--------------------------------------------------------------------------------------------------------
# VAPI configuration

GASNET_IF_ENABLED(vapi, Enable the VAPI (Mellanox InfiniBand) network conduit, 
  have_vapi=yes,
  have_vapi=no)

if test $have_vapi = yes; then
  GASNET_ENV_DEFAULT(VAPI_INCLUDE, "$MTHOME/include")
  GASNET_ENV_DEFAULT(VAPI_LIBDIR, "$MTHOME/lib")
  GASNET_ENV_DEFAULT(VAPI_LIBS, -lvapi -lmtl_common -lmosal -lpthread -lmpga)

  AC_MSG_CHECKING(for working VAPI configuration)
  if test "$mpi_test_worked" != yes; then
    if test "$enabled_mpi" = yes; then
      AC_MSG_ERROR([launching VAPI programs requires MPI on your system, but MPI was not sucessfuly configured.]);
    else
      AC_MSG_ERROR([launching VAPI programs requires MPI on your system, but you have disabled it.]);
    fi
  fi

  OLDCFLAGS="$CFLAGS"
  OLDLIBS="$LIBS"
  CFLAGS="-I$VAPI_INCLUDE"
  LIBS="-L$VAPI_LIBDIR $VAPI_LIBS"
  # try to compile and link a simple VAPI program
  AC_TRY_LINK( [
    #include <evapi.h>
  ], [{
    int rc;
    u_int32_t num_of_hcas;
    VAPI_hca_id_t inst_hca_id;

    rc = EVAPI_list_hcas(1, &num_of_hcas, &inst_hca_id);
    }], [ vapi_test_worked=yes ], [ vapi_test_worked=no ] )
  CFLAGS="$OLDCFLAGS"
  LIBS="$OLDLIBS"

  if test "$vapi_test_worked" = yes; then
    AC_MSG_RESULT(yes)
  else
    AC_MSG_RESULT(no)
    AC_MSG_ERROR(build test failed: I don't know how to build VAPI programs on your system)
    have_vapi=no
  fi

  GASNET_IF_ENABLED_WITH_AUTO(vapi-inline-puts, [See vapi-conduit/README (enabled by default)], 
    enabled_vapi_inline_puts=yes, enabled_vapi_inline_puts=no, enabled_vapi_inline_puts=yes)
    if test "$enabled_vapi_inline_puts" = yes; then
      AC_DEFINE(GASNETC_VAPI_ENABLE_INLINE_PUTS)
    fi

  GASNET_IF_ENABLED_WITH_AUTO(vapi-force-poll-lock, [See vapi-conduit/README (disabled by default)], 
    enabled_vapi_force_poll_lock=yes, enabled_vapi_force_poll_lock=no, enabled_vapi_force_poll_lock=no)
    if test "$enabled_vapi_force_poll_lock" = yes; then
      AC_DEFINE(GASNETC_VAPI_FORCE_POLL_LOCK)
    fi
fi

AC_SUBST(VAPI_INCLUDE)
AC_SUBST(VAPI_LIBDIR)
AC_SUBST(VAPI_LIBS)
AM_CONDITIONAL(USE_VAPI_CONDUIT, test "$have_vapi" = yes)

########################################################################

# Check for broken asm/atomic.h on Linux
case "$target_os" in
  linux*)
    AC_MSG_CHECKING(for broken asm/atomic.h)
    AC_TRY_LINK( [
      #include <asm/atomic.h>
    ], [
      atomic_t v;
      atomic_set(&v,5);
      atomic_inc(&v);
    ], AC_MSG_RESULT(ok), 
    [ AC_MSG_RESULT(broken) 
      AC_DEFINE(BROKEN_LINUX_ASM_ATOMIC_H)
    ] )
    ;;
esac

# Check for alloca in C code
AC_MSG_CHECKING(for alloca in C code)
ALLOCA_IN_C=""
AC_TRY_LINK([
/* AIX requires this to be the first thing in the file.  */
#ifndef __GNUC__
# if HAVE_ALLOCA_H
#  include <alloca.h>
# else
#  ifdef _AIX
 #pragma alloca
#  else
#   ifndef alloca /* predefined by HP cc +Olibcalls */
char *alloca ();
#   endif
#  endif
# endif
#endif
], [
char *p = (char *) alloca(10);
], [ alloca_in_c=yes ], [ alloca_in_c=no ])
if test "$alloca_in_c" = yes; then
  AC_MSG_RESULT(yes)
  ALLOCA_IN_C="1"
else
  AC_MSG_RESULT(no)
fi
AC_SUBST(ALLOCA_IN_C)

# -lm should only live in LIBM, and only if we have it (not in LIBS)
OLDLIBS="$LIBS"
# sin should be in everyone's libm if they've got one.
AC_CHECK_LIB(m, sin, LIBM="-lm", LIBM="")
AC_SUBST(LIBS)
AC_SUBST(LIBM)

LIBS="$OLDLIBS"

AC_CHECK_FUNCS(setenv unsetenv putenv)

########################################################################

# Try to discover the C compiler's inline modifier

inlinemod=""
case "$gasnet_cv_cc_family" in
  Compaq) # force __inline on Compaq C, because it's the most effective
    AC_DEFINE(CC_INLINE_MODIFIER, __inline)
    inlinemod=__inline
  ;;
esac
if test "$inlinemod" = ""; then
  GASNET_TRY_CACHE_CHECK(for inline modifier, cc_modinline,
    [inline int dummy() { return 1; }], [],
    AC_DEFINE(CC_INLINE_MODIFIER, inline)
    inlinemod=inline)
fi
if test "$inlinemod" = ""; then
  GASNET_TRY_CACHE_CHECK(for __inline__ modifier, cc_mod__inline__,
    [__inline__ int dummy() { return 1; }], [],
    AC_DEFINE(CC_INLINE_MODIFIER, __inline__)
    inlinemod=__inline__)
fi
if test "$inlinemod" = ""; then
  GASNET_TRY_CACHE_CHECK(for __inline modifier, cc_mod__inline,
    [__inline int dummy() { return 1; }], [],
    AC_DEFINE(CC_INLINE_MODIFIER, __inline)
    inlinemod=__inline)
fi
if test "$inlinemod" = ""; then
  GASNET_TRY_CACHE_CHECK(for _inline modifier, cc_mod_inline,
    [_inline int dummy() { return 1; }], [],
    AC_DEFINE(CC_INLINE_MODIFIER, _inline)
    inlinemod=_inline)
fi
if test "$inlinemod" = ""; then
  GASNET_TRY_CACHE_CHECK(for _Inline modifier, cc_mod_Inline,
    [_Inline int dummy() { return 1; }], [],
    AC_DEFINE(CC_INLINE_MODIFIER, _Inline)
    inlinemod=_Inline)
fi

if test "$inlinemod" != ""; then
  AC_MSG_CHECKING(whether static $inlinemod works)
  AC_TRY_COMPILE([static $inlinemod int dummy() { return 1; }], [],
    [AC_DEFINE(STATIC_INLINE_WORKS)
     AC_MSG_RESULT(yes)],
    AC_MSG_RESULT(no))
fi

# Runtime Var Arrays
#GASNET_IF_ENABLED_WITH_AUTO(vararray, Use stack arrays of variable size in code,
#AC_DEFINE(VARARRAY_WORKS),
#,
#GASNET_TRY_CACHE_CHECK(for variable stack arrays, cc_vararray_mod,
#  [void dummy(int x) { char y[x]; }], [],
#  AC_DEFINE(VARARRAY_WORKS)))

########################################################################
# different high-precision sleep libraries

# Check for usleep
GASNET_TRY_CACHE_CHECK(for usleep(), cc_has_usleep, [
#include <unistd.h>
], [
usleep(500);
],
  AC_SEARCH_LIBS(usleep, posix4, AC_DEFINE(HAVE_USLEEP), AC_MSG_RESULT(entry point not found - disabled)))

# Check for nanosleep
GASNET_TRY_CACHE_CHECK(for nanosleep(), cc_has_nanosleep, [
#include <time.h>
#include <sys/time.h>
], [
  struct timespec tm, tmremaining;
  tm.tv_sec =1;
  tm.tv_nsec = 1000000;
  nanosleep(&tm, &tmremaining);
],
  AC_SEARCH_LIBS(nanosleep, posix4, AC_DEFINE(HAVE_NANOSLEEP), AC_MSG_RESULT(entry point not found - disabled)))

# Check for nsleep
GASNET_TRY_CACHE_CHECK(for nsleep(), cc_has_nsleep, [
#include <time.h>
#include <sys/time.h>
], [
  struct timespec tm, tmremaining;
  tm.tv_sec =1;
  tm.tv_nsec = 1000000;
  nsleep(&tm, &tmremaining);
],
  AC_SEARCH_LIBS(nsleep, posix4, AC_DEFINE(HAVE_NSLEEP), AC_MSG_RESULT(entry point not found - disabled)))

# Check for sched_yield 
GASNET_TRY_CACHE_CHECK(for sched_yield(), cc_has_sched_yield, [
#include <sched.h>
], [
  sched_yield();
],
  AC_SEARCH_LIBS(sched_yield, posix4, AC_DEFINE(HAVE_SCHED_YIELD), AC_MSG_RESULT(entry point not found - disabled)))

#  AC_FUNC_MMAP
# Check for mmap functionality we actually use
case "$target_os" in
  cygwin*)  
  # mmap is known to be broken on Cygwin - dont even try
  ;;
  *)
GASNET_TRY_CACHE_RUN(for working mmap(), have_mmap, [
  #include <unistd.h>
  #include <sys/mman.h>
  #include <sys/types.h>
  #include <sys/stat.h>
  #include <fcntl.h>
  #include <stdio.h>
  char junk[[16384]];
  int testfd(int);
  int main() {
    int fd, retval;
    #if 0
      char filename[[255]];
      tmpnam(filename); /* unsafe */
      fd = open(filename, O_RDWR | O_CREAT);
    #else
      char filename[[255]];
      strcpy(filename,"/tmp/gasnet-conftemp-XXXXXX");
      fd = mkstemp(filename); /* leaves crap laying around */
    #endif
    retval = testfd(fd);
    close(fd);
    remove(filename);
    return retval; 
  }
  int testfd(int fd) {
    void *ptr,*ptr2;
    if (fd == -1) return 1;
    if (write(fd, junk, 16384) == -1) return 2;
    ptr = mmap(0, 16384, (PROT_READ|PROT_WRITE), 
	MAP_PRIVATE, fd, 0);
    if (ptr == MAP_FAILED || ptr == NULL) return 3;
    if (munmap(ptr,16384) != 0) return 4;
    ptr2 = mmap(ptr, 16384, (PROT_READ|PROT_WRITE), 
	(MAP_PRIVATE | MAP_FIXED), fd, 0);
    if (ptr2 == MAP_FAILED || ptr2 == NULL || ptr2 != ptr) return 5;
    if (munmap(ptr,16384) != 0) return 6;
    return 0;
  }],[AC_DEFINE(HAVE_MMAP)])
  ;;
esac

AC_SEARCH_LIBS(pthread_kill_other_threads_np, pthread pthreads, AC_DEFINE(HAVE_PTHREAD_KILL_OTHER_THREADS_NP))

AC_SUBST(LIBS)

########################################################################
# Special GCC feature support

# Check for __builtin_expect (GCC 3.0) 
GASNET_TRY_CACHE_LINK(for __builtin_expect, cc_has_builtin_expect,
  [int x;], [if (__builtin_expect(x,1)) return 1;],
  AC_DEFINE(HAVE_BUILTIN_EXPECT))

GASNET_TRY_CACHE_LINK(for __func__, cc_has_func,
[],
[const char* p = __func__;],
AC_DEFINE(HAVE_FUNC))


########################################################################
##
##  Supporting Command-Line Tools (continued)
##

dnl Use the results of AC_CHECK_SIZEOF(void *) to select 64-bit mode for some tools
dnl NOTE: AC_CHECK_SIZEOF may only be safely called after AC_PROG_CC/AC_PROG_CPP
case "$target_os" in
    aix*)
        # If on AIX, and in 64 bit mode, we need to use 'ranlib -X64' and 'ar -X64'
        if test x"$ac_cv_sizeof_void_p" = x8; then
            AR="ar -X64"
            RANLIB="ranlib -X64"
        fi
        ;;
    osf*)
        # Tru64 has a default /bin/sh that emits error msgs for missing
        # commands, even when 2>/dev/null passed.  Use posix shell instead.
        if test -x /usr/bin/posix/sh; then
            BOURNE_SHELL="/usr/bin/posix/sh"
        fi
        ;;
esac
dnl     Store full paths, so we find right ones even if users have
dnl     some other version in their path
if test x"$BOURNE_SHELL" = x; then
    BOURNE_SHELL="/bin/sh"
fi

GASNET_PATH_PROGS(PERL, perl5 perl, perl)
MIN_PERL_VERSION="5.005"
AC_MSG_CHECKING(for perl version $MIN_PERL_VERSION or later)
if $PERL -e "require $MIN_PERL_VERSION;" 2>&AC_FD_CC; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_ERROR(cannot find perl $MIN_PERL_VERSION or later)
fi
GASNET_PATH_PROGS(AR, $AR ar gar, archiver)
GASNET_PATH_PROGS(RANLIB, $RANLIB ranlib, ranlib)
AC_SUBST(BOURNE_SHELL)
AC_SUBST(PERL)
AC_SUBST(AR)
AC_SUBST(RANLIB)

########################################################################

# complete sp2 setup

case "$target_os" in
  aix*)
    # AIX linker doesn't allow large enough TOC by default to accomodate
    # most large applications
    # LDFLAGS="$LDFLAGS -Wl,-bbigtoc"

    # set the data segment size to the largest permitted size for 32-bit apps (2GB)
    # MLW: No.  (1) this has no effect when building libraries, only executables
    #               and should be under control of end user.
    #           (2) If this was used for a executable in a 32 bit app there would
    #               only be one segment left for mmap and shared memory segments
    #               and GASNET allocs shared mem region using mmap.
    # LDFLAGS="$LDFLAGS -Wl,-bmaxdata:0x80000000"
  ;;
esac
AC_SUBST(LDFLAGS)

PERLSTART=$TOP_SRCDIR/other/perlstart
AC_SUBST_FILE(PERLSTART)

# Figure out how to install config files
GASNET_SET_INSTALL_CMD

########################################################################
##
##  Final Output
##

GASNET_SAVE_AUTOCONF_ENV()

GASNET_FIX_EXEC(gm-conduit/contrib/gasnetrun_gm)
GASNET_FIX_EXEC(mpi-conduit/contrib/gasnetrun_mpi)
GASNET_FIX_EXEC(vapi-conduit/contrib/gasnetrun_vapi)

# Files added here should also be added to ./unBootstrap

AC_OUTPUT(
Makefile

mpi-conduit/Makefile
mpi-conduit/mpi-seq.mak
mpi-conduit/mpi-parsync.mak
mpi-conduit/mpi-par.mak
mpi-conduit/contrib/Makefile
mpi-conduit/contrib/gasnetrun_mpi

udp-conduit/Makefile
udp-conduit/udp-seq.mak
udp-conduit/udp-parsync.mak
udp-conduit/udp-par.mak

gm-conduit/Makefile
gm-conduit/gm-seq.mak
gm-conduit/gm-parsync.mak
gm-conduit/gm-par.mak
gm-conduit/contrib/Makefile
gm-conduit/contrib/gasnetrun_gm

lapi-conduit/Makefile
lapi-conduit/lapi-seq.mak
lapi-conduit/lapi-parsync.mak
lapi-conduit/lapi-par.mak

smp-conduit/Makefile
smp-conduit/smp-seq.mak
smp-conduit/smp-parsync.mak
smp-conduit/smp-par.mak

elan-conduit/Makefile
elan-conduit/elan-seq.mak
elan-conduit/elan-parsync.mak
elan-conduit/elan-par.mak

vapi-conduit/Makefile
vapi-conduit/vapi-seq.mak
vapi-conduit/vapi-parsync.mak
vapi-conduit/vapi-par.mak
vapi-conduit/contrib/Makefile
vapi-conduit/contrib/gasnetrun_vapi

other/Makefile
other/ammpi/Makefile
other/amudp/Makefile
tests/Makefile
config-aux/Makefile
,
GASNET_FIX_EXEC_OUTPUT(), GASNET_FIX_EXEC_SETUP()
)

